CS224
Lab No. 01 Preliminary
Section No. 001
Full Name: Halil Arda Ã–zongun
Bilkent ID 22202709
Date 09.10.2024

----------------------------------------------------------------------------

1. Swap array elements (15 points):

#
# Halil Arda Ozongun ID: 22202709
# CS 224- Lab1 
# Solution of question 1 in preliminary part
#
	

	.data 
askinput: .asciiz "Enter the size of array please: "
asknumber: .asciiz "Enter the array element: "
newline: .asciiz  "\n"
space: .asciiz " "
printinitialarray: .asciiz "Array before changes: "
printfinalarrayword: .asciiz "Array after changes: "
array: .align 4  # Ensure word alignment
       .space 80 # allocate max, 4*20

# t0 -> input, array size
# t1 -> counter, to count the current element. initialize with 0
# t2 -> 4*counter, so it is the current bit in array
# t3 -> array adress
# t4 -> current element in array

.text	j main
main:
	# want number of elements
	li $v0, 4
	la $a0, askinput
    	syscall
    	
    	# read number of elements and store it
    	li $v0, 5
    	syscall
    	move $t0, $v0 # array size in t0
    	
    	# make a counter, take input and count until input size is done.
    	add $t1, $zero, $zero # initialize counter
    	
arrayinput:
	bge $t1, $t0, arrayinputdone # check if array input is done
	
	li $v0, 4
	la $a0, asknumber
    	syscall
    	# take number
    	
    	li $v0, 5
    	syscall
    	# store it in to array
    	# Each integer is 4 bytes, so we calculate the offset as t1 * 4
    	sll $t2, $t1, 2   # t2 = t1 * 4
    	la $t3, array     # load base address of array
    	add $t3, $t3, $t2 # t3 = base + offset
    	sw $v0, 0($t3)    # store the input at array[t1]
    	addi $t1, $t1, 1    	# Increment the counter
	j arrayinput

arrayinputdone:     
	add $t1, $zero, $zero # make counter is zero
	li $v0, 4
	la $a0, printinitialarray
	syscall
	
printarray:
	bge $t1, $t0, swap_part

	sll $t2, $t1, 2 # t2 = t1 * 4 
	la $t3, array     # load base address of array
	add $t3, $t3, $t2 # t3 = base + offset
	lw $a0, 0($t3)    # load the array element into $a0
	
	li $v0, 1         # syscall for print integer
	syscall
	
	li $v0, 4         # syscall for print string
	la $a0, space   # load newline string
	syscall

	addi $t1, $t1, 1 	# increment counter
	j printarray
	
swap_part:
	# t1 is start pointer t5 is end pointer
	li $t1, 0
	addi $t5, $t0, -1 
	
	# print newline
	li $v0, 4
    	la $a0, newline
    	syscall

swaploop:
	bge $t1, $t5, swapdone

	# Load elements at t1 and t5
	sll $t2, $t1, 2
	la $t3, array
	add $t3, $t3, $t2
	lw $t6, ($t3)  # t6 = array[t1]
    
	sll $t2, $t5, 2
	la $t3, array
	add $t3, $t3, $t2
	lw $t7, ($t3)  # t7 = array[t5]
    
	# Swap elements
	sll $t2, $t1, 2
	la $t3, array
	add $t3, $t3, $t2
	sw $t7, ($t3)  # array[t1] = t7
    
	sll $t2, $t5, 2
	la $t3, array
	add $t3, $t3, $t2
	sw $t6, ($t3)  # array[t5] = t6
    
	addi $t1, $t1, 1
	subi $t5, $t5, 1

	j swaploop
	
swapdone:
	li $t1, 0 
	li $v0, 4
	la $a0, printfinalarrayword
	syscall

printfinalarray:
	bge $t1, $t0, exit

	sll $t2, $t1, 2 # t2 = t1 * 4 
	la $t3, array     # load base address of array
	add $t3, $t3, $t2 # t3 = base + offset
	lw $a0, 0($t3)    # load the array element into $a0
	
	li $v0, 1         # syscall for print integer
	syscall
	
	li $v0, 4         # syscall for print string
	la $a0, space   # load newline string
	syscall

	addi $t1, $t1, 1 	# increment counter
	j printfinalarray

exit:
    	li $v0, 4
    	la $a0, newline
    	syscall
    
    	# Exit program
    	li $v0, 10
    	syscall



----------------------------------------------------------------------------
2. Calculate an arithmetic expression (15 points)
 
#
# Halil Arda Ozongun ID: 22202709
# CS 224- Lab1 
# Solution of question 2 in preliminary part
#
		.data

prompt_B:  .asciiz "Enter value for B: "
prompt_C:  .asciiz "Enter value for C: "
prompt_D:  .asciiz "Enter value for D: "
result:    .asciiz "Result is: "

# $s0 -> B
# $s1 -> C
# $s2 -> D

.text
    .globl main

main:

    # Prompt for B
    li $v0, 4
    la $a0, prompt_B
    syscall
    li $v0, 5
    syscall
    move $s0, $v0            # store B in $s0
    
    # Prompt for C
    li $v0, 4 
    la $a0, prompt_C         
    syscall
    li $v0, 5  
    syscall
    move $s1, $v0            # store C in $s1
    
    # Prompt for D
    li $v0, 4       
    la $a0, prompt_D        
    syscall
    li $v0, 5
    syscall
    move $s2, $v0            # store D in $s2


    # calculation is: A= (B / C + D Mod B - C ) / B and then print A
    
    # B/C
    move $a0, $s0
    move $a1, $s1
    jal divide
    # hold B/C in t0
    move $t0, $v0

    # D Mod B
    move $a0, $s2
    move $a1, $s0
    jal mod
    # hold D Mod B in t1
    move $t1, $v0

    # B/C + D Mod B
    add $t0, $t0, $t1
    
    # (B/C + D Mod B) - C
    sub $t0, $t0, $s1

    # ((B/C + D Mod B - C) / B)
    move $a0, $t0
    move $a1, $s0
    jal divide
    move $t0, $v0

    # print and finish
    li $v0, 4
    la $a0, result
    syscall 
    li $v0, 1                
    move $a0, $t0
    syscall
    li $v0, 10
    syscall
    
    
mod: # this function takes a mod b, a is in $a0, $a1 and returns to the answer into $v0
	move $v0, $a0
mod_loop:
	blt $v0, $a1, mode_done
	sub $v0, $v0, $a1
	j mod_loop
mode_done:
	jr $ra



divide: # this function takes a / b, a is in $a0, $a1 and returns to the quotient into v0 and remaining into v1
	li $v0, 0 # quotient to 0
	move $t0, $a0

divide_loop:
	blt $t0, $a1, divide_done
	sub $t0, $t0, $a1
	addi $v0, $v0, 1
	j divide_loop


divide_done:
	jr $ra
